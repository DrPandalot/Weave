<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<ui:DraggablePanel xmlns="weave.ui.settings.*" 
				   xmlns:mx="http://www.adobe.com/2006/mxml" 
				   xmlns:ui="weave.ui.*" 
				   sessionPanelCoordsAsPercentages="false"
				   minWidth="300"
				   percentWidth="NaN" percentHeight="NaN"
				   title="{lang('Export Session State')}"
				   >
	<ui:ExportSessionStateOptionsList id="optionsList"/>
	<mx:ControlBar>
		<mx:Button label="{lang('Export')}" click="export()" toolTip="{lang('Export session with these options')}"/>
		<mx:Button label="{lang('Cancel')}" click="removePanel()"/>
	</mx:ControlBar>
	<mx:Script>
		<![CDATA[
			import mx.formatters.DateFormatter;
			import mx.utils.StringUtil;
			
			import weave.Weave;
			import weave.api.WeaveArchive;
			import weave.api.linkBindableProperty;
			import weave.api.reportError;
			import weave.compiler.StandardLib;
			import weave.core.LinkableBoolean;
			import weave.core.SessionManager;
			import weave.core.SessionStateLog;
			import weave.editors.SessionHistorySlider;
			
			
			private var oldValues:Dictionary = new Dictionary(true);
			
			override protected function constructor():void
			{
				super.constructor();
				
				for each (var lb:LinkableBoolean in [
					Weave.properties.dashboardMode,
					Weave.properties.enableMenuBar,
					Weave.properties.showCopyright
				])
					oldValues[lb] = lb.value;
			}
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();
				
				optionsList.saveHistoryCheckBox.visible = Weave.history.enableLogging.value;
				optionsList.saveHistoryCheckBox.selected = Weave.history.enableLogging.value;
				if(Weave.getScreenshotFromArchive())
				{
					optionsList.saveScreenshot.selected = true;
				}
			}
			
			private function zeropad(n:Number):String
			{
				return StandardLib.lpad('' + n, 2, "0");
			}
			
			private function export():void
			{
				// Create a date that we can append to the end of each file to make them unique
				var date:Date = new Date();
				
				var format:DateFormatter = new DateFormatter();
				format.formatString = "YYYY-MM-DD_HH.NN.SS";

				var fileName:String = 'Weave_' + format.format(date);
				
				var content:Object = null;
				var extension:String = '.weave';
				if (optionsList.saveXMLCheckBox.selected)
				{
					content = Weave.getSessionStateXML().toXMLString();
					extension = '.xml';
				}
				else if (!optionsList.saveHistoryCheckBox.selected)
				{
					var historySessionState:Object = Weave.history.getSessionState();
					Weave.history.clearHistory();
					content = Weave.createWeaveFileContent(optionsList.saveScreenshot.selected);
					Weave.history.setSessionState(historySessionState);
				}
				else
				{
					content = Weave.createWeaveFileContent(optionsList.saveScreenshot.selected);
				}
				var fr:FileReference = new FileReference();
				fr.addEventListener(IOErrorEvent.IO_ERROR, reportError);
				fr.save(content, fileName + extension);
				
				removePanel();
			}
			
			override public function removePanel():void
			{
				super.removePanel();
				
				// restore settings
				for (var lb:* in oldValues)
					(lb as LinkableBoolean).value = oldValues[lb];
			}
		]]>
	</mx:Script>
</ui:DraggablePanel>
