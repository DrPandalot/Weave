<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" 
		 xmlns="weave.ui.*"
		 verticalGap="0" creationPolicy="all"
		 width="100%" height="100%"
		 implements="weave.api.core.ILinkableObject">
	<mx:HBox width="100%" id="titleHBox" horizontalGap="0">
		<mx:Label text="{label}" fontWeight="bold"/>
		<HelpComponent content="{helpContent}" visible="{!!helpContent}" includeInLayout="{!!helpContent}"/>
		<mx:HBox width="100%" id="selectorControls">
			<mx:Spacer width="100%"/>
			<mx:Button label="{lang('Open selector...')}" click="openSelector()"/>
		</mx:HBox>
	</mx:HBox>
	<!-- internalList will be added as a child here -->
	<mx:HBox width="100%" verticalAlign="middle">
		<CenteredImage id="reverseButton" buttonMode="true" source="@Embed(source='/weave/resources/images/reverse-order.png')"
						width="22" height="22"
						toolTip="{lang('Reverse the order of the attributes.')}"
						click="controller.hashMap.setNameOrder(controller.hashMap.getNames().reverse());"/>
		<mx:Spacer width="100%"/>
		<mx:Button
			label="{lang('Remove all')}"
			toolTip="{lang('Click here to remove all attributes from the list.')}"
			click="controller.removeAllItems()"
			enabled="{_internalList.dataProvider.length > 0}"/>
		<mx:Button
			label="{lang('Remove selected')}"
			toolTip="{lang('Click here to remove one or more selected attributes in the list above.')}"
			click="controller.removeSelectedItems()"
			enabled="{_internalList.selectedIndex >= 0}"/>
	</mx:HBox>
	<mx:Script>
		<![CDATA[
			import avmplus.getQualifiedClassName;
			
			import mx.collections.ICollectionView;
			import mx.controls.DataGrid;
			import mx.controls.List;
			import mx.controls.listClasses.ListBase;
			
			import weave.api.core.ILinkableHashMap;
			import weave.api.core.ILinkableObject;
			import weave.api.data.IAttributeColumn;
			import weave.api.getCallbackCollection;
			import weave.api.newLinkableChild;
			import weave.api.ui.IVisToolWithSelectableAttributes;
			import weave.core.LinkableWatcher;
			import weave.utils.ColumnUtils;
			
			[Bindable] public var helpContent:*;
			
			/**
			 * This code will be executed at the time the object is being created.
			 */
			private var _constructor:* = function():void
			{
				// title is set here instead of in the mxml because if it is set through binding,
				// it will overwrite any other value set in mxml components based on this class.
				label = lang('Attributes');
			}();
			
			
			override protected function createChildren():void
			{
				super.createChildren();
				showVariableNames = showVariableNames;
				controller.labelFunction = getItemLabel;
				getCallbackCollection(controller).addGroupedCallback(this, function():void {
					reverseButton.buttonMode = reverseButton.enabled = (
						controller.view
						&& controller.view.dataProvider is ICollectionView
						&& ICollectionView(controller.view.dataProvider).length > 1
					);
					reverseButton.alpha = reverseButton.enabled ? .8 : 0.25;
				}, true);
			}
			
			private function getItemLabel(item:*):String
			{
				return item is IAttributeColumn
					? ColumnUtils.getColumnListLabel(item)
					: getQualifiedClassName(item).split('::').pop();
			}
			
			[Bindable] private var _internalList:ListBase;
			private const _internalListChildIndex:int = 1;
			private const controller:VariableListController = newLinkableChild(this, VariableListController);
			private const toolWatcher:LinkableWatcher = newLinkableChild(this, LinkableWatcher, updateLabel, true);
			
			public function get showVariableNames():Boolean
			{
				return controller.view is DataGrid;
			}
			
			public function set showVariableNames(value:Boolean):void
			{
				_internalList = value
					? controller.view as DataGrid || new DataGrid()
					: controller.view as List || new List();
				
				if (controller.view != _internalList)
				{
					if (controller.view && controller.view.parent == this)
						removeChild(controller.view);
					
					controller.view = _internalList;
					
					_internalList.percentWidth = 100;
					_internalList.percentHeight = 100;
					_internalList.invalidateDisplayList();
				}
				
				if (!controller.view.parent && numChildren >= _internalListChildIndex)
					addChildAt(controller.view, _internalListChildIndex);
			}
			
			public function beginEditVariableName(object:ILinkableObject):void
			{
				controller.beginEditVariableName(object);
			}
			
			private function updateLabel():void
			{
				if (!controller.view)
				{
					callLater(updateLabel);
					return;
				}
				var tool:IVisToolWithSelectableAttributes = toolWatcher.target as IVisToolWithSelectableAttributes;
				if (!tool)
					return;
				var names:Array = tool.getSelectableAttributeNames();
				var attrs:Array = tool.getSelectableAttributes();
				var index:int = attrs.indexOf(controller.hashMap);
				if (index >= 0)
					label = names[index];
			}
			
			private function openSelector():void
			{
				AttributeSelectorPanel.open(controller.hashMap, label, showVariableNames);
			}
			
			public function set hashMap(value:ILinkableHashMap):void
			{
				controller.hashMap = value;
				toolWatcher.target = AttributeSelectorPanel.getVisTool(value);
				updateLabel();
			}
			public function get hashMap():ILinkableHashMap
			{
				return controller.hashMap;
			}
		]]>
	</mx:Script>
</mx:VBox>
