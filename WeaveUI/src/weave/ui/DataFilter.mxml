<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<DraggablePanel xmlns="weave.ui.*" 
				layout="vertical" verticalAlign="middle"
				xmlns:mx="http://www.adobe.com/2006/mxml" 
				xmlns:ui="weave.ui.*" implements="weave.api.ui.IVisTool_Utility, weave.api.ui.ISelectableAttributes, weave.api.core.ILinkableObjectWithNewProperties"
				horizontalScrollPolicy="off" verticalScrollPolicy="off"
				>
	<mx:Script>
		<![CDATA[
			import weave.Weave;
			import weave.api.core.ICallbackCollection;
			import weave.api.core.ILinkableObject;
			import weave.api.data.IAttributeColumn;
			import weave.api.data.IKeyFilter;
			import weave.api.getCallbackCollection;
			import weave.api.getSessionState;
			import weave.api.newLinkableChild;
			import weave.api.registerLinkableChild;
			import weave.api.setSessionState;
			import weave.api.ui.ILinkableObjectEditor;
			import weave.api.ui.IVisTool;
			import weave.core.LinkableBoolean;
			import weave.core.LinkableDynamicObject;
			import weave.core.LinkableWatcher;
			import weave.data.KeySets.NumberDataFilter;
			import weave.data.KeySets.StringDataFilter;
			import weave.editors.StringDataFilterEditor;
			import weave.menus.WeaveMenuItem;
			import weave.utils.ColumnUtils;
			
			WeaveAPI.ClassRegistry.registerImplementation(IVisTool, DataFilter, "Data Filter");
			
			public const editor:LinkableDynamicObject = registerLinkableChild(this, new LinkableDynamicObject(ILinkableObjectEditor), handleEditor, true);
			
			private function getEditor():ILinkableObjectEditor
			{
				return editor.internalObject as ILinkableObjectEditor;
			}
			private function handleEditor():void
			{
				var _editor:ILinkableObjectEditor = getEditor();
				if (_editor && !_editor.parent)
					addChild(_editor as DisplayObject);
			}
			
			private var _editorStateMissing:Boolean = false;

			private var _globalName:String;
			private const _filterWatcher:LinkableWatcher = newLinkableChild(this, LinkableWatcher, handleFilterWatcher);
			
			private function handleFilterWatcher():void
			{
				var _editor:ILinkableObjectEditor = getEditor();
				if (!_editor)
				{
					var editorClass:Class = WeaveAPI.EditorManager.getEditorClass(keyFilter);
					if (editorClass)
					{
						_editor = editor.requestLocalObject(editorClass, false);
						if (_editorStateMissing && _editor is StringDataFilterEditor)
						{
							_editorStateMissing = false;
							(_editor as StringDataFilterEditor).layoutMode.value = "ComboBox";
						}
					}
				}
				if (_editor)
					_editor.setTarget(keyFilter);

				handlePanelTitleChange();
			}
			
			private function get globalName():String
			{
				return _globalName || (_globalName = WeaveAPI.globalHashMap.getName(this));
			}
			
			private function get keyFilter():IKeyFilter
			{
				return Weave.defaultSubsetKeyFilter.filters.getObject(globalName) as IKeyFilter;
			}
			
			private function get filterColumn():IAttributeColumn
			{
				var kf:IKeyFilter = keyFilter;
				var ndf:NumberDataFilter = kf as NumberDataFilter;
				var sdf:StringDataFilter = kf as StringDataFilter;
				return ((ndf && ndf.column) || (sdf && sdf.column)) as IAttributeColumn;
			}
			
			override protected function inConstructor():void
			{
				super.inConstructor();
				enableSubMenu.value = true;
			}
			
			override protected function createChildren():void
			{
				if (createdChildren)
					return;
				super.createChildren();
				
				function onClick(item:WeaveMenuItem):void
				{
					var filterType:Class = item.data[0] as Class;
					var layoutMode:String = item.data[1];
					
					setFilterType(filterType);
					
					var editor:StringDataFilterEditor = getEditor() as StringDataFilterEditor;
					if (editor)
						editor.layoutMode.value = layoutMode;
				}
				function isToggled(item:WeaveMenuItem):Boolean
				{
					var filterType:Class = item.data[0] as Class;
					var layoutMode:String = item.data[1];
					if (Weave.defaultSubsetKeyFilter.filters.getObject(globalName) is filterType)
					{
						if (layoutMode)
						{
							var editor:StringDataFilterEditor = getEditor() as StringDataFilterEditor;
							return editor && editor.layoutMode.value == layoutMode;
						}
						return true;
					}
					return false;
				}
				subMenuButton.data = [
					{
						data: [StringDataFilter],
						label: lang("String filter"),
						type: WeaveMenuItem.TYPE_RADIO,
						toggled: isToggled,
						children: MenuToolViewStack.LAYOUTS.map(function(layout:String, i:int, a:Array):Object {
							return {
								data: [StringDataFilter, layout],
								label: MenuToolViewStack.layoutLabelFunction(layout),
								click: onClick,
								type: WeaveMenuItem.TYPE_RADIO,
								toggled: isToggled
							};
						})
					},{
						data: [NumberDataFilter],
						label: lang("Number filter"),
						click: onClick,
						type: WeaveMenuItem.TYPE_RADIO,
						toggled: isToggled
					},
					WeaveMenuItem.TYPE_SEPARATOR,
					{
						label: lang("Show filter toggle CheckBox"),
						type: WeaveMenuItem.TYPE_RADIO,
						groupName: 'showToggle',
						toggled: function():Boolean {
							var _editor:ILinkableObjectEditor = getEditor();
							return _editor && getSessionState(_editor)['showToggle'];
						},
						click: function():void {
							var _editor:ILinkableObjectEditor = getEditor();
							if (_editor)
								setSessionState(_editor, {showToggle: !getSessionState(_editor)['showToggle']}, false);
						}
					},
					WeaveMenuItem.TYPE_SEPARATOR,
					{
						label: lang("Edit session state"),
						click: toggleControlPanel
					}
				];
				
				Weave.defaultSubsetKeyFilter.filters.childListCallbacks.addImmediateCallback(this, handleFilters, true);
				if (!keyFilter)
					setFilterType(StringDataFilter);
			}
			
			override public function dispose():void
			{
				Weave.defaultSubsetKeyFilter.filters.removeObject(globalName);
				super.dispose()
			}
			
			private function setFilterType(type:Class):void
			{
				var editorState:Object = getEditor() && getSessionState(editor.internalObject);
				var filterState:Object = keyFilter && getSessionState(keyFilter);
				editor.removeObject();
				
				var fwcc:ICallbackCollection = getCallbackCollection(_filterWatcher);
				fwcc.delayCallbacks();
				
				Weave.defaultSubsetKeyFilter.filters.requestObject(globalName, type, false);
				if (filterState)
					setSessionState(keyFilter, filterState);
				
				fwcc.triggerCallbacks();
				fwcc.resumeCallbacks();
				
				if (editor.internalObject)
					setSessionState(editor.internalObject, editorState);
			}
			
			private function handleFilters():void
			{
				_filterWatcher.target = keyFilter;
			}
			
			override public function get defaultPanelTitle():String
			{
				if (filterColumn)
					return lang('Filter for {0}', ColumnUtils.getTitle(filterColumn));
				
				return lang('Filter');
			}
			
			public function getSelectableAttributeNames():Array
			{
				return filterColumn ? ['Filter'] : [];
			}
			
			public function getSelectableAttributes():Array
			{
				return filterColumn ? [filterColumn] : [];
			}
			
			public function handleMissingSessionStateProperty(newState:Object, missingProperty:String):void
			{
				if (missingProperty == 'editor')
				{
					_editorStateMissing = true;
					handleFilterWatcher();
				}
			}
		]]>
	</mx:Script>
</DraggablePanel>
