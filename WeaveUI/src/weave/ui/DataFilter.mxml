<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<DraggablePanel xmlns="weave.ui.*" 
				layout="vertical" verticalAlign="middle"
				xmlns:mx="http://www.adobe.com/2006/mxml" 
				xmlns:ui="weave.ui.*" implements="weave.api.ui.IVisTool_Utility, weave.api.ui.ISelectableAttributes"
				horizontalScrollPolicy="off" verticalScrollPolicy="off"
				>
	<mx:Script>
		<![CDATA[
			import weave.Weave;
			import weave.api.core.ILinkableObject;
			import weave.api.data.IAttributeColumn;
			import weave.api.data.IKeyFilter;
			import weave.api.getSessionState;
			import weave.api.newLinkableChild;
			import weave.api.registerLinkableChild;
			import weave.api.setSessionState;
			import weave.api.ui.ILinkableObjectEditor;
			import weave.api.ui.IVisTool;
			import weave.core.LinkableDynamicObject;
			import weave.core.LinkableWatcher;
			import weave.data.KeySets.NumberDataFilter;
			import weave.data.KeySets.StringDataFilter;
			import weave.menus.WeaveMenuItem;
			import weave.utils.ColumnUtils;
			
			WeaveAPI.ClassRegistry.registerImplementation(IVisTool, DataFilter, "Data Filter");
			
			public const editor:LinkableDynamicObject = registerLinkableChild(this, new LinkableDynamicObject(ILinkableObjectEditor), handleEditor, true);
			
			private function getEditor():ILinkableObjectEditor
			{
				return editor.internalObject as ILinkableObjectEditor;
			}
			private function handleEditor():void
			{
				var _editor:ILinkableObjectEditor = getEditor();
				if (!_editor.parent)
					addChild(_editor as DisplayObject);
			}

			private var _globalName:String;
			private const _filterWatcher:LinkableWatcher = newLinkableChild(this, LinkableWatcher, handleFilterWatcher, true);
			
			private function handleFilterWatcher():void
			{
				var _editor:ILinkableObjectEditor = getEditor();
				if (!_editor)
				{
					var editorClass:Class = WeaveAPI.EditorManager.getEditorClass(keyFilter);
					if (editorClass)
						_editor = editor.requestLocalObject(editorClass, false);
				}
				if (_editor)
					_editor.setTarget(keyFilter);

				handlePanelTitleChange();
			}
			
			private function get globalName():String
			{
				return _globalName || (_globalName = WeaveAPI.globalHashMap.getName(this));
			}
			
			private function get keyFilter():IKeyFilter
			{
				return Weave.defaultSubsetKeyFilter.filters.getObject(globalName) as IKeyFilter;
			}
			
			private function get filterColumn():IAttributeColumn
			{
				var kf:IKeyFilter = keyFilter;
				var ndf:NumberDataFilter = kf as NumberDataFilter;
				var sdf:StringDataFilter = kf as StringDataFilter;
				return ((ndf && ndf.column) || (sdf && sdf.column)) as IAttributeColumn;
			}
			
			override protected function inConstructor():void
			{
				super.inConstructor();
				enableSubMenu.value = true;
			}
			
			override protected function createChildren():void
			{
				if (createdChildren)
					return;
				super.createChildren();
				
				function onClick(item:WeaveMenuItem):void { setFilterType(item.data as Class); }
				function isToggled(item:WeaveMenuItem):Boolean { return Weave.defaultSubsetKeyFilter.filters.getObject(globalName) is (item.data as Class); }
				subMenuButton.data = [
					{
						data: StringDataFilter,
						label: lang("String filter (Combo box)"),
						click: onClick,
						type: WeaveMenuItem.TYPE_RADIO,
						toggled: isToggled
					},{
						data: NumberDataFilter,
						label: lang("Number filter (Dual slider)"),
						click: onClick,
						type: WeaveMenuItem.TYPE_RADIO,
						toggled: isToggled
					}
				];
				
				Weave.defaultSubsetKeyFilter.filters.childListCallbacks.addImmediateCallback(this, handleFilters, true);
				if (!keyFilter)
					setFilterType(StringDataFilter);
			}
			
			override public function dispose():void
			{
				Weave.defaultSubsetKeyFilter.filters.removeObject(globalName);
				super.dispose()
			}
			
			private function setFilterType(type:Class):void
			{
				editor.removeObject();
				var state:Object = filterColumn && getSessionState(filterColumn);
				Weave.defaultSubsetKeyFilter.filters.requestObject(globalName, type, false) as IKeyFilter;
				if (filterColumn && state)
					setSessionState(filterColumn, state);
			}
			
			private function handleFilters():void
			{
				_filterWatcher.target = keyFilter;
			}
			
			override public function get defaultPanelTitle():String
			{
				if (filterColumn)
					return lang('Filter for {0}', ColumnUtils.getTitle(filterColumn));
				
				return lang('Filter');
			}
			
			public function getSelectableAttributeNames():Array
			{
				return filterColumn ? ['Filter'] : [];
			}
			
			public function getSelectableAttributes():Array
			{
				return filterColumn ? [filterColumn] : [];
			}
		]]>
	</mx:Script>
</DraggablePanel>
