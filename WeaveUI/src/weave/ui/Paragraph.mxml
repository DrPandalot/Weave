<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<mx:TextArea xmlns="weave.ui.*" xmlns:mx="http://www.adobe.com/2006/mxml"
    verticalScrollPolicy="off" horizontalScrollPolicy="off"
	editable="false"
	borderStyle="none"
	width="100%"
	wordWrap="true"
	backgroundAlpha="0"
	selectable="false"
	focusEnabled="false"
    >
	<mx:Metadata>
		[DefaultProperty("content")]
	</mx:Metadata>
    <mx:Script>
        <![CDATA[
			import mx.core.mx_internal;
			import mx.utils.StringUtil;
			
			import weave.compiler.StandardLib;
			
			private var _untranslated:String = '';
			private var _trim:Boolean = true;
			private var _translate:Boolean = false;
			
			/**
			 * Setting this to true will remove all tab characters and replace new lines with spaces.
			 */
			public function set trim(value:Boolean):void
			{
				if (_trim != value)
				{
					_trim = value;
					text = _untranslated;
				}
			}
			
			/**
			 * Setting this to true will automatically call lang() whenever the text is set.
			 */
			public function set translate(value:Boolean):void
			{
				if (_translate != value)
				{
					_translate = value;
					text = _untranslated;
				}
			}
			
			[Exclude]
			public function set content(value:*):void
			{
				text = value as String;
			}
			
			[Bindable("textChanged")]
			[CollapseWhiteSpace]
			[Inspectable(category="General", defaultValue="")]
			[NonCommittingChangeEvent("change")]
			override public function get text():String
			{
				return super.text;
			}
			
			override public function set text(value:String):void
			{
				value = value || ''; // not null
				_untranslated = value;
				if (_trim)
				{
					function trimItem(str:*, ..._):* { return StringUtil.trim(str) || '\n'; }
					value = value.split('\n').map(trimItem).join(' ');
					value = StringUtil.trim(StandardLib.replace(value, ' \n ', '\n\n'));
				}
				if (_translate)
					value = lang(value);
				super.text = value;
			}
			
			override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
			{
				// call this first to update textField properties
				super.updateDisplayList(unscaledWidth, unscaledHeight);
				
				// update width,height based on textField width,height
				var text:String = textField.text;
				textField.height = 0;
				textField.autoSize = 'center';
            	if (wordWrap)
            	{
	            	textField.width;
            	}
            	else
            	{
					width = Math.round(textField.width);
            	}
				if (width)
	                height = Math.round(textField.height);
				
				mx_internal::invalidateSizeFlag = false;
				mx_internal::invalidatePropertiesFlag = false;
				mx_internal::invalidateDisplayListFlag = false;
				
				// call this again because width or height may have changed
				super.updateDisplayList(unscaledWidth, unscaledHeight);
			}
        ]]>
    </mx:Script>
</mx:TextArea>
