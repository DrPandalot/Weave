<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<CustomComboBox xmlns:mx="http://www.adobe.com/2006/mxml"
		xmlns="weave.ui.*"
		textDecoration="none"
		fontStyle="normal"
		fontWeight="normal"
		rowCount="12"
		dropdownWidth="250"
		creationComplete="creationComplete();">
	<mx:Script>
		<![CDATA[
			import weave.WeaveProperties;
			
			private function creationComplete():void
			{
				dropdown.variableRowHeight = true;
			}
			
			private var _embeddedOnly:Boolean = false;
			private var _fontsInvalid:Boolean = true;
			
			public function set embeddedFontsOnly(value:Boolean):void
			{
				if (_embeddedOnly != value)
				{
					_embeddedOnly = value;
					_fontsInvalid = true;
					invalidateProperties();
				}
			}
			
			private static var _allFonts:Array = null;
			
			override public function validateProperties():void
			{
				if (_fontsInvalid)
				{
					_fontsInvalid = false;
					if (_embeddedOnly)
					{
						dataProvider = WeaveProperties.embeddedFonts;
					}
					else
					{
						if (!_allFonts)
							_allFonts = Font.enumerateFonts(true).sortOn("fontName");
						dataProvider = _allFonts;
						labelField = "fontName";
					}
				}
				super.validateProperties();
			}
		]]>
	</mx:Script>
	<itemRenderer>
		<mx:Component>
			<mx:Label>
				<mx:Script>
					<![CDATA[
						override public function set data(value:Object):void
						{
							var font:Font = value as Font;
							var fontName:String = font ? font.fontName : String(value);
							setStyle("fontFamily", fontName);
							setStyle("fontSize", 15);
							text = fontName;
							super.data = value;
						}
					]]>
				</mx:Script>
			</mx:Label>
		</mx:Component>
	</itemRenderer>
</CustomComboBox>
