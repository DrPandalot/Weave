<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->

<!---
@author abaumann
@author sanbalagan
@author adufilie
-->
<mx:VBox xmlns:ui ="weave.ui.*"
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns="weave.ui.*"
	implements="weave.api.ui.ILinkableObjectEditor,weave.api.core.ILinkableObject"
	width="100%" height="100%"
	enabled="{colorRamp != null}"
	>
	<mx:HBox height="100%" width="100%">
		<mx:VBox width="100%" height="100%" id="listVBox">
			<ui:ColorRampList id="colorRampList" colorRamp="{colorRamp}"/>
			<ui:Indent label="{lang('Filter:')}">
				<ui:CustomComboBox id="filterCombo" change="(colorRampList.dataProvider as ICollectionView).refresh();"/>
			</ui:Indent>
		</mx:VBox>
		<mx:VBox height="100%">
			<mx:Label text="{lang('Customize')}" fontWeight="bold"/>
			<mx:HBox height="100%" width="100%" horizontalGap="0">
				<mx:Canvas id="colorRampCanvas" resize="if (colorRamp) colorRamp.draw(colorRampCanvas, true)"
						   borderStyle="solid" borderColor="0x000000"
						   width="20" height="100%"/>
				<mx:List id="colorsList"
						 height="100%"
						 width="150"
						 borderStyle="none"
						 dragEnabled="true"
						 dropEnabled="true"
						 dragMoveEnabled="true"
						 dragComplete="updateNewColorScale()"
						 initialize="UIUtils.pad(event, 2);">
					<mx:itemRenderer>
						<mx:Component>
							<mx:HBox verticalAlign="middle" horizontalScrollPolicy="off" width="100%">
								<ui:CustomColorPicker id="cPicker" showTextField="true"/>
								<ui:TextInputWithPrompt borderStyle="none" editable="false" id="cText" backgroundAlpha="0" width="100%"/>
								<mx:Image source="@Embed(source='/weave/resources/images/cancel.png')" buttonMode="true"
										   click="outerDocument.removeColorItem();"/>
								<mx:Script>
									<![CDATA[
										import mx.collections.ArrayCollection;
										import mx.events.DragEvent;
										
										import weave.compiler.StandardLib;
										import weave.core.UIUtils;
										
										override protected function childrenCreated():void
										{
											super.childrenCreated();
											
											cPicker.addEventListener(Event.CHANGE, handleColorPicked);
											cText.addEventListener(Event.CHANGE, handleColorPicked);
											outerDocument.colorsList.addEventListener(DragEvent.DRAG_START, handleDragStart);
										}
										
										override public function set data(value:Object):void
										{
											super.data = value;
											
											if (data)
											{
												var number:Number = StandardLib.asNumber(data.color);
												var string:String = outerDocument.colorToString(number);
												
												cPicker.selectedColor = number;
												cText.text = string;
											}
										}
										
										private function handleColorPicked(event:Event):void
										{
											if (event.currentTarget == cPicker)
												data.color = outerDocument.colorToString(cPicker.selectedColor);
											else
												data.color = outerDocument.colorToString(cText.text);
											outerDocument.updateNewColorScale();
										}
										
										private function handleDragStart(e:Event):void
										{
											cPicker.close();
											cText.setSelection(0, 0);
											setFocus(); // stops selecting text
										}
									]]>
								</mx:Script>
							</mx:HBox>
						</mx:Component>
					</mx:itemRenderer>
				</mx:List>
			</mx:HBox>
			<mx:HBox width="100%">
				<CenteredImage id="reverseButton" buttonMode="true" source="@Embed(source='/weave/resources/images/reverse-order.png')"
							   width="22" height="22" alpha="0.8"
							   toolTip="{lang('Reverse the order of the colors.')}"
							   click="colorRamp.reverse();"/>
				<mx:Spacer width="100%"/>
				<mx:Button label="{lang('Add color')}" click="(colorsList.dataProvider as ArrayCollection).addItem({color:'0xFFFFFF'}); updateNewColorScale();"/>
			</mx:HBox>
		</mx:VBox>
	</mx:HBox>
	<mx:Script>
		<![CDATA[
			import weave.api.registerLinkableChild;
			import weave.api.newLinkableChild;
			import weave.core.LinkableWatcher;
			import weave.core.UIUtils;
			import weave.utils.AsyncSort;
			import mx.events.DragEvent;
			import mx.collections.ICollectionView;
			import mx.collections.IList;
			import mx.collections.ListCollectionView;
			import weave.compiler.StandardLib;
			import mx.collections.ArrayCollection;
			import mx.collections.XMLListCollection;
			
			import weave.api.core.ILinkableObject;
			import weave.primitives.ColorRamp;
			
			private const colorRampWatcher:LinkableWatcher = registerLinkableChild(this, new LinkableWatcher(ColorRamp), handleColorRampChange, true);
			[Bindable]
			public function get colorRamp():ColorRamp { return colorRampWatcher.target as ColorRamp; }
			public function set colorRamp(value:ColorRamp):void { colorRampWatcher.target = value; }
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();
				
				var tagsLookup:Object = {};
				var tags:Array = [];
				for each (var xml:XML in ColorRamp.allColorRamps.colorRamp)
					for each (var tag:String in String(xml.@tags).split(','))
						if (!tagsLookup[tag])
							tags.push(tagsLookup[tag] = tag);
				AsyncSort.sortImmediately(tags, AsyncSort.compareCaseInsensitive);
				tags.unshift(ALL);
				filterCombo.dataProvider = tags;
				colorRampList.dataProvider.filterFunction = filterColormaps;
			}
			
			private const ALL:String = lang('All');
			
			private function filterColormaps(item:Object):Boolean
			{
				var filter:String = filterCombo.selectedItem as String;
				return (filter == ALL || String(item.@tags).split(',').indexOf(filter) >= 0);
			}
			
			public function removeColorItem():void
			{
				(colorsList.dataProvider as ArrayCollection).removeItemAt(colorsList.selectedIndex);
				updateNewColorScale();
			}
			
			internal function colorToString(color:*):String
			{
				color = StandardLib.asNumber(color);
				return '0x' + StandardLib.numberToBase(color, 16, 6).toUpperCase();
			}
			
			private function handleColorRampChange():void
			{
				if (!initialized)
				{
					callLater(handleColorRampChange)
					return;
				}
				
				if (colorsList.dataProvider == null)
					colorsList.dataProvider = [];
				
				var dp:ArrayCollection = (colorsList.dataProvider as ArrayCollection);
				dp.removeAll();
				var colors:Array = colorRamp.getColors();
				for (var i:int = 0; i < colors.length; i++)
				{
					var item:Object = new Object();
					item.color = colorToString(colors[i]);
					dp.addItem(item);
				}
				
				item = ColorRamp.getColorRampXMLByColorString(colorRamp.value)
				colorRampList.selectedItem = item;
				if (item)
					colorRampList.scrollToIndex(colorRampList.selectedIndex);
				
				colorRamp.draw(colorRampCanvas, true);
			}
			
			public function updateNewColorScale():void
			{
				var rampStringValue:String = '';
				
				for (var i:int = 0; i < colorsList.dataProvider.length; i++)
				{
					var item:Object = (colorsList.dataProvider as ArrayCollection).getItemAt(i);
					
					rampStringValue += item.color + ',';
				}
				
				rampStringValue = rampStringValue.substr(0,rampStringValue.length-1);
				
				colorRamp.value = rampStringValue;
			}
			public function setTarget(object:ILinkableObject):void
			{
				colorRamp = object as ColorRamp;
			}
			public function hasPendingChanges():Boolean { return false; }
			public function applyChanges():void { }
		]]>
	</mx:Script>
</mx:VBox>
