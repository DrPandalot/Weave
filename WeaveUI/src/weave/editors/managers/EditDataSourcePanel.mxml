<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<ui:DraggablePanel
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns="weave.editors.managers.*"
	xmlns:ui="weave.ui.*"
	backgroundColor="#EFF4BC"
	width="700"
	height="500"
	escapeKeyClosesPanel="true"
	sessionPanelCoordsAsPercentages="false"
	title="{lang('Manage data sources')}" xmlns:editors="weave.editors.*">
	<mx:HDividedBox initialize="UIUtils.pad(event, padding, 100, 100)">
		<mx:VBox height="100%">
			<mx:Button label="{lang('New data source...')}" click="addSource()" width="100%"/>
			<ui:Paragraph translate="true">
				Select a data source:
			</ui:Paragraph>
			<mx:List id="sourceList"
					 change="handleSourceSelect()"
					 minHeight="0"
					 height="100%" width="100%">
				<mx:itemRenderer>
					<mx:Component>
						<mx:Canvas>
							<mx:Script>
								<![CDATA[
									import weave.api.WeaveAPI;
									import weave.api.core.ILinkableObject;
								]]>
							</mx:Script>
							<mx:Label text="{WeaveAPI.globalHashMap.getName(data as ILinkableObject)}"/>
							<ui:BusyIndicator targets="{[data]}" diameterRatio=".75"/>
						</mx:Canvas>
					</mx:Component>
				</mx:itemRenderer>
			</mx:List>
			<mx:Button label="{lang('Remove selected')}" click="removeSelected()" width="100%"/>
		</mx:VBox>
		<mx:TabNavigator id="tabNav" initialize="UIUtils.pad(event, padding, 100, 100)">
			<mx:ViewStack id="browseTab" label="{lang('Browse')}" initialize="UIUtils.pad(event, 0, 100, 100, true)" selectedIndex="{dataSource ? 1 : 0}">
				<mx:Canvas width="100%" height="100%">
					<ui:Paragraph translate="true" id="browseMessage"/>
				</mx:Canvas>
				<editors:DynamicColumnEditor id="browser" dataSource="{dataSource}"/>
			</mx:ViewStack>
			<mx:ViewStack id="editTab" label="{lang('Configure')}" initialize="UIUtils.pad(event, 0, 100, 100, true)" selectedIndex="{dataSource ? 1 : 0}">
				<mx:Canvas width="100%" height="100%">
					<ui:Paragraph translate="true" id="configureMessage"/>
				</mx:Canvas>
				<mx:VBox width="100%" height="100%">
					<mx:Box id="container" initialize="UIUtils.pad(event, 0, 100, 100, true)"/>
					<mx:HBox width="100%">
						<mx:Spacer width="100%"/>
						<mx:Button label="{lang('Apply changes')}" click="save()"/>
						<mx:Button label="{lang('Cancel')}" click="selectDataSource(dataSource); tabNav.selectedChild = browseTab;"/>
					</mx:HBox>
				</mx:VBox>
			</mx:ViewStack>
		</mx:TabNavigator>
	</mx:HDividedBox>
		
		<mx:Script>
			<![CDATA[
				import weave.api.setSessionState;
				import weave.api.disposeObjects;
				import weave.api.registerDisposableChild;
				import weave.utils.PopUpUtils;
				import weave.api.reportError;
				import mx.managers.PopUpManager;
				
				import weave.api.WeaveAPI;
				import weave.api.core.ILinkableObject;
				import weave.api.data.IDataSource;
				import weave.api.ui.ILinkableObjectEditor;
				import weave.core.UIUtils;
				import weave.utils.EditorManager;
				
				private static const padding:int = 8;
				
				override protected function childrenCreated():void
				{
					super.childrenCreated();
					
					pinnable.value = false;
					WeaveAPI.globalHashMap.childListCallbacks.addImmediateCallback(this, updateSources, true);
					selectDataSource(null);
					tabNav.addEventListener(Event.CHANGE, handleTabIndexChange);
				}
				
				private function handleTabIndexChange(event:Event):void
				{
					if (event)
					{
						// if we try to change the selectedChild immediately, the tab nav gets out of sync
						callLater(handleTabIndexChange, [null]);
						return;
					}
					if (tabNav.selectedChild != editTab && hasPendingChanges())
					{
						var newTabIndex:int = tabNav.selectedIndex;
						tabNav.selectedChild = editTab;
						confirmDiscard(function():void {
							selectDataSource(dataSource); // resets changes
							tabNav.selectedIndex = newTabIndex;
						});
					}
				}
				
				private function hasPendingChanges():Boolean
				{
					return editor && editor.parent && editor.hasPendingChanges();
				}
				
				private function updateSources():void
				{
					sourceList.dataProvider = WeaveAPI.globalHashMap.getObjects(IDataSource);
					sourceList.selectedItem = dataSource;
					
					if (WeaveAPI.globalHashMap.childListCallbacks.lastObjectRemoved == dataSource)
						selectDataSource(null);
					
					var newDataSource:IDataSource = WeaveAPI.globalHashMap.childListCallbacks.lastObjectAdded as IDataSource;
					if (newDataSource && !hasPendingChanges())
					{
						selectDataSource(newDataSource);
						tabNav.selectedIndex = 0;
					}
				}
				
				[Bindable] private var dataSource:IDataSource;
				private var editor:ILinkableObjectEditor = null;
				
				private function selectDataSource(value:IDataSource):void
				{
					sourceList.selectedItem = value;
					handleSourceSelect(true);
				}
				
				private function handleSourceSelect(confirmed:Boolean = false):void
				{
					var newDataSource:IDataSource = sourceList.selectedItem as IDataSource;
					if (!confirmed && dataSource == newDataSource)
						return;
					
					if (!confirmed && hasPendingChanges())
					{
						// switch back until user confirms
						sourceList.selectedItem = dataSource;
						tabNav.selectedIndex = 1;
						confirmDiscard(function():void { selectDataSource(newDataSource); });
						return;
					}
					
					if (editor && editor.parent == container)
						container.removeChild(editor as DisplayObject);
					disposeObjects(editor);
					editor = null;
					
					dataSource = newDataSource
					if (dataSource)
					{
						editor = registerDisposableChild(this, EditorManager.getNewEditor(dataSource));
						if (!editor)
							return;
						container.addChildAt(editor as DisplayObject, 0);
						editor.percentWidth = 100;
						editor.percentHeight = 100;
					}
					else
					{
						if (WeaveAPI.globalHashMap.getObjects(IDataSource).length)
							browseMessage.text = configureMessage.text = "Please select a data source.";
						else
							browseMessage.text = configureMessage.text = "Please create a data source first.";
					}
				}
				private function addSource():void
				{
					DraggablePanel.openStaticInstance(AddDataSourcePanel);
				}
				private function removeSelected():void
				{
					WeaveAPI.globalHashMap.removeObject(WeaveAPI.globalHashMap.getName(dataSource));
				}
				
				private function save():void
				{
					try
					{
						if (editor != null)
						{
							editor.applyChanges();
							setSessionState(dataSource.attributeHierarchy, null);
							tabNav.selectedChild = browseTab;
						}
					}
					catch (e:Error)
					{
						reportError(e);
					}
				}
				
				override public function removePanel():void
				{
					if (hasPendingChanges())
					{
						confirmDiscard(function():void {
							selectDataSource(null);
							removePanel();
						});
					}
					else
					{
						selectDataSource(null);
						super.removePanel();
					}
				}
				
				private function confirmDiscard(yes:Function):void
				{
					PopUpUtils.confirm(
						this,
						lang('Discard changes'),
						lang('Discard unsaved changes?'),
						yes, null,
						lang('Discard'), lang('Cancel')
					);
				}
			]]>
		</mx:Script>
</ui:DraggablePanel>