<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->

<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" 
		 xmlns="weave.editors.managers.*"
		 width="100%" height="100%"
		 creationPolicy="all"
		 label="{lang('Layers')}" xmlns:ui="weave.ui.*">
	<ui:Indent id="layerButtons" grouped="false" label="{lang('Visualization layers')}" fontWeight="bold" verticalAlign="bottom">
		<mx:HBox width="100%" id="selectorControls">
			<mx:Spacer width="100%"/>
			<mx:Button id="addButton" label="{lang('New layer...')}" click="addLayer()"/>
			<mx:Button id="removeButton"
				label="{lang('Remove selected')}"
				toolTip="{lang('Remove the selected layers.')}"
				click="removeSelectedLayers()"
				enabled="{list.selectedIndex >= 0}"/>
		</mx:HBox>
	</ui:Indent>
	<mx:VBox borderStyle="solid" initialize="UIUtils.pad(event, 5, 100, 100)">
		<mx:HBox width="100%" verticalAlign="middle" toolTip="{lang('Foreground layers appear at the beginning of this list.')}">
			<mx:HRule width="100%"/>
			<mx:Label text="{lang('Foreground')}" alpha="0.5"/>
			<mx:HRule width="100%"/>
		</mx:HBox>
		<mx:List id="list" variableRowHeight="true" initialize="UIUtils.pad(event, 2, 100, 100)" borderStyle="none">
			<mx:itemRenderer>
				<mx:Component>
					<mx:HBox width="100%" horizontalScrollPolicy="off" verticalScrollPolicy="off"
							 resize="(parent as UIComponent).invalidateSize();"
							 addedToStage="(parent as UIComponent).invalidateSize();">
						<mx:Script>
							<![CDATA[
								import mx.core.UIComponent;
								import mx.managers.PopUpManager;
								
								import weave.api.getSessionState;
								import weave.api.linkBindableProperty;
								import weave.api.objectWasDisposed;
								import weave.api.setSessionState;
								import weave.api.ui.IPlotter;
								import weave.api.unlinkBindableProperty;
								import weave.ui.AlertTextBox;
								import weave.ui.AlertTextBoxEvent;
								import weave.ui.ControlPanel;
								import weave.ui.VariableListController;
								import weave.visualization.layers.LayerSettings;
								import weave.visualization.layers.PlotManager;
								import weave.visualization.layers.Visualization;
								
								private function get visualization():Visualization
								{
									return outerDocument.visualization;
								}
								private var plotter:IPlotter;
								private var layerSettings:LayerSettings;
								
								private function openSettingsPanel(plotter:IPlotter):void
								{
									ControlPanel.openEditor(plotter, [visualization]);
								}
								
								[Bindable] public var layerIsVisible:Boolean = true;
								[Bindable] public var layerIsSelectable:Boolean = true;
								override public function set data(newPlotter:Object):void
								{
									// filter out bogus data
									if (objectWasDisposed(newPlotter))
										newPlotter = null;
									
									newPlotter = newPlotter as IPlotter;
									
									lbl.text = outerDocument.list.itemToLabel(newPlotter);
									callLater((parent as UIComponent).invalidateSize);
									
									if (plotter == newPlotter)
										return;
									
									// unlink checkboxes
									if (plotter)
									{
										unlinkBindableProperty(layerSettings.visible, this, "layerIsVisible");
										unlinkBindableProperty(layerSettings.selectable, this, "layerIsSelectable");
									}
									
									// save pointers
									super.data = newPlotter;
									plotter = data as IPlotter;
									var pm:PlotManager = outerDocument.visualization.plotManager;
									var name:String = pm.plotters.getName(plotter);
									layerSettings = pm.getLayerSettings(name);
									
									// link checkboxes
									if (plotter)
									{
										linkBindableProperty(layerSettings.visible, this, "layerIsVisible");
										linkBindableProperty(layerSettings.selectable, this, "layerIsSelectable");
									}
									
									rename.enabled = !pm.plotters.objectIsLocked(name);
								}
								
								protected function onLayerRenameButtonClick(event:MouseEvent,data:Object):void
								{
									var defaultInputString:String = outerDocument.controller.getItemName(data);
									var alertBox:AlertTextBox = AlertTextBox.show(lang("Rename Layer"), lang("Please enter a new layer name:"), defaultInputString);
									
									alertBox.addEventListener(AlertTextBoxEvent.BUTTON_CLICKED, 
										function (e:AlertTextBoxEvent):void 
										{
											// if the user clicked cancel, then we will just return from here and save nothing
											if( !e.confirm ) return;
											
											//Alert.show("Input was " + e.textInput);
											var pm:PlotManager = outerDocument._visualization.plotManager;
											var ls:LayerSettings = pm.layerSettings.getObject(defaultInputString) as LayerSettings;
											var settings:Object = getSessionState(ls);
											pm.plotters.renameObject(defaultInputString, e.textInput);
											ls = pm.layerSettings.getObject(e.textInput) as LayerSettings;
											setSessionState(ls, settings);
										}
									);
								}
								
							]]>
						</mx:Script>
						<ui:CustomCheckBox label="{lang('Visible')}" selected="{layerIsVisible}" change="layerIsVisible = event.target.selected"/>
						<ui:CustomCheckBox label="{lang('Selectable')}" selected="{layerIsSelectable}" change="layerIsSelectable = event.target.selected"/>
						<ui:Paragraph id="lbl" textAlign="center"/>
						<mx:Button id="rename" label="{lang('Rename')}" toolTip="{lang('Click to rename the layer.')}" click="onLayerRenameButtonClick(event,data)"/>
						<mx:Button label="{lang('Edit properties')}" click="openSettingsPanel(data as IPlotter)"/>
					</mx:HBox>
				</mx:Component>
			</mx:itemRenderer>
		</mx:List>
		<mx:Label id="listOverlay"
				  width="{list.width}" y="{(list.height - listOverlay.height) / 2}"
				  mouseChildren="false" alpha="0.5"
				  textAlign="center"
				  text="{lang('Start by adding a new layer.')}"
				  creationComplete="list.addChild(listOverlay);"
				  visible="{isEmpty(list.dataProvider)}"/>
		<mx:HBox width="100%" verticalAlign="middle" toolTip="{lang('Background layers appear at the end of this list.')}">
			<mx:HRule width="100%"/>
			<mx:Label text="{lang('Background')}" alpha="0.5"/>
			<mx:HRule width="100%"/>
		</mx:HBox>
	</mx:VBox>
	<!--
	<mx:CheckBox id="enableAutoZoom" label="{lang('Enable auto zoom to extent')}"
				 creationComplete="linkBindableProperty(_visTool.visualization.enableAutoZoomToExtent, event.target, 'selected');"/>
	<mx:CheckBox label="{lang('Enable zoom and pan')}"
				 creationComplete="linkBindableProperty(_visTool.visualization.enableZoomAndPan, event.target, 'selected');"/>
	-->
	<ui:Indent label="{lang('Override auto zoom:')}">
		<ui:TextInputWithPrompt id="overrideXMin" prompt="{lang('xMin')}" width="100%"/>
		<mx:VBox width="100%">
			<ui:TextInputWithPrompt id="overrideYMax" prompt="{lang('yMax')}" width="100%"/>
			<ui:TextInputWithPrompt id="overrideYMin" prompt="{lang('yMin')}" width="100%"/>
		</mx:VBox>
		<ui:TextInputWithPrompt id="overrideXMax" prompt="{lang('xMax')}" width="100%"/>
		<mx:Button label="{lang('Use current zoom')}" click="setOverrideCoords(true)"/>
		<mx:Button label="{lang('Reset')}" click="setOverrideCoords(false)"/>
	</ui:Indent>
	<mx:Script>
		<![CDATA[
			import weave.api.ui.IPlotter;
			import weave.core.UIUtils;
			import weave.primitives.Bounds2D;
			import weave.visualization.layers.PlotManager;
			import weave.api.newDisposableChild;
			import weave.ui.VariableListController;
			import weave.visualization.layers.Visualization;
			import mx.managers.PopUpManager;
			import weave.api.core.ILinkableHashMap;
			import weave.ui.SessionStateEditor;
			import weave.api.linkBindableProperty;

			private function isEmpty(ac:*):Boolean
			{
				return !ac || !ac.length;
			}
			
			[Bindable] internal var _visualization:Visualization = null;
			public const controller:VariableListController = newDisposableChild(this, VariableListController);
			
			public function set labelFunction(value:Function):void
			{
				controller.labelFunction = value;
			}
			
			public function get selectedPlotterName():String
			{
				return controller.hashMap.getName(list.selectedItem as IPlotter);
			}
			
			public function get selectedPlotter():IPlotter
			{
				return list.selectedItem as IPlotter;
			}
			
			[Inspectable]
			public function set visualization(value:Visualization):void
			{
				_visualization = value;
				controller.hashMap = _visualization.plotManager.plotters;
			}
			public function get visualization():Visualization
			{
				return _visualization;
			}
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();
				
				if (_visualization)
				{
					linkBindableProperty(_visualization.plotManager.overrideXMin, overrideXMin, 'text');
					linkBindableProperty(_visualization.plotManager.overrideYMin, overrideYMin, 'text');
					linkBindableProperty(_visualization.plotManager.overrideXMax, overrideXMax, 'text');
					linkBindableProperty(_visualization.plotManager.overrideYMax, overrideYMax, 'text');
				}
				
				controller.reverse = true;
				controller.view = list;
			}
			
			private function setOverrideCoords(setOrUnset:Boolean):void
			{
				var pm:PlotManager = _visualization.plotManager;
				if (setOrUnset)
				{
					var b:Bounds2D = new Bounds2D();
					pm.zoomBounds.getDataBounds(b);
					pm.overrideXMin.value = b.xMin;
					pm.overrideYMin.value = b.yMin;
					pm.overrideXMax.value = b.xMax;
					pm.overrideYMax.value = b.yMax;
				}
				else
				{
					pm.overrideXMin.value = NaN;
					pm.overrideYMin.value = NaN;
					pm.overrideXMax.value = NaN;
					pm.overrideYMax.value = NaN;
				}
			}
			
			[Bindable]
			private var _minimumNumObjectsInList:int = -1;
			public function set minimumNumObjectsInList(value:int):void
			{
				_minimumNumObjectsInList = value;
			}
			
			[Inspectable]
			public function set dragMoveEnabled(value:Boolean):void
			{
				// this value has to be stored for later because internalList may be null
				_dragMoveEnabled = value;
			}
			[Bindable] private var _dragMoveEnabled:Boolean = false;

			private var _addLayerPopup:CustomLayerWindow;
			public function addLayer():void
			{
				if (!_addLayerPopup)
				{
					_addLayerPopup = newDisposableChild(this, CustomLayerWindow);
					_addLayerPopup.targetVisualization = _visualization;
				}
				
				_addLayerPopup.restorePanel();
				if (!_addLayerPopup.parent)
					PopUpManager.addPopUp(_addLayerPopup, WeaveAPI.topLevelApplication as DisplayObject);
			}
			public function removeSelectedLayers():void
			{
				controller.removeSelectedItems();
			}
		]]>
	</mx:Script>
</mx:VBox>
