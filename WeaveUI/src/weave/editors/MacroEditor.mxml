<?xml version="1.0" encoding="utf-8"?>
<!--
/*
Weave (Web-based Analysis and Visualization Environment)
Copyright (C) 2008-2011 University of Massachusetts Lowell

This file is a part of Weave.

Weave is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License, Version 3,
as published by the Free Software Foundation.

Weave is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->

<!---
@author jfallon
-->
<ui:ControlPanel xmlns:mx="http://www.adobe.com/2006/mxml"
				 xmlns:ui="weave.ui.*"
				 backgroundColor="#EFF4BC"
				 title="{lang('Macro Editor')}">
	<mx:VBox width="100%" height="100%" paddingBottom="5" paddingLeft="5" paddingRight="5"
			 paddingTop="5">
		<mx:HBox>
			<mx:Button width="33%" height="100%" label="{lang('Add')}" click="newMacroEntry(event)"/>
			<mx:Button width="33%" height="100%" label="{lang('Delete')}" click="deleteEntry(event)"
					   enabled="{buttonsEnabled}"/>
			<mx:Button width="33%" height="100%" label="{lang('Rename')}"
					   click="renameMacroEntry(event)" enabled="{buttonsEnabled}"/>
		</mx:HBox>
		<mx:HDividedBox width="100%" height="100%">
			<mx:VBox width="25%" height="100%">
				<mx:List id="macroList" width="100%" height="100%" itemClick="macroList_itemClickHandler(event)"/>
			</mx:VBox>
			<mx:VDividedBox width="75%" height="100%">
				<ui:CodeEditor id="editor"/>
				<mx:VBox height="25%" width="100%">
					<mx:Text text="{lang('Macro Result:')}"/>
					<mx:TextArea id="macroResult" width="100%" height="100%" minHeight="24" editable="false"/>
				</mx:VBox>
			</mx:VDividedBox>
		</mx:HDividedBox>
	</mx:VBox>
	<mx:Script>
		<![CDATA[
			import mx.controls.Alert;
			import mx.events.ListEvent;
			
			import weave.api.getCallbackCollection;
			import weave.api.newLinkableChild;
			import weave.core.LinkableFunction;
			import weave.ui.AlertTextBox;
			import weave.ui.AlertTextBoxEvent;
			import weave.ui.VariableListController;
			import weave.utils.EventUtils;
			
			//Used for enabling/disabling the delete and rename button based on whether this macroList has at least one item.
			[Bindable]
			private var buttonsEnabled:Boolean = false;
			private var controller:VariableListController = newLinkableChild(this, VariableListController);
			
			override protected function constructor():void
			{
				super.constructor();
				
				minimizable.value = true;
				maximizable.value = true;
			}
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();
				
				controller.hashMap = LinkableFunction.macros;
				controller.allowMultipleSelection = false;
				controller.view = macroList;
				
				if (controller.hashMap.getNames().length > 0)
				{
					buttonsEnabled = true;
					macroList.selectedIndex = 0;
					editor.text = selectedFunction.value;
					handleChange();
				}
				this.targets = [controller.hashMap];
				getCallbackCollection(controller.hashMap).addImmediateCallback(this, enableButtonsCheck, true);
				EventUtils.addDelayedEventCallback(editor, Event.CHANGE, handleChange);
			}
			
			public function get selectedFunction():LinkableFunction
			{
				return macroList.selectedItem as LinkableFunction
					|| (macroList.selectedIndex >= 0
						? controller.hashMap.getObjects()[macroList.selectedIndex]
						: null);
			}
			
			public function get selectedFunctionName():String
			{
				return controller.hashMap.getName(selectedFunction);
			}
			
			protected function deleteEntry(event:MouseEvent):void
			{
				if (selectedFunction)
				{
					controller.hashMap.removeObject(selectedFunctionName);
			
				    if (controller.hashMap.getNames().length > 0)
			    	{
				    	macroList.selectedIndex = 0;
					    editor.text = selectedFunction.value;
						try
						{
							macroResult.text = LinkableFunction.evaluateMacro(selectedFunctionName);
						}
						catch(e:Error)
						{
							macroResult.text = e.toString();
						}
				    }
				    else
					{
						editor.text = "";
						macroResult.text = "";
					}
					if (controller.hashMap.getNames().length == 0)
						editor.editable = false;
				}
			}
			
			protected function newMacroEntry(event:MouseEvent):void
			{
				var box:AlertTextBox = AlertTextBox.show(lang("Macro Naming Window"), lang("Enter a name for the new macro:"));
				box.addEventListener(AlertTextBoxEvent.BUTTON_CLICKED,checkInput);
				macroResult.text = "";
			}
			
			private function checkInput(event:AlertTextBoxEvent):void
			{
				if( event.confirm == true )
				{
					var macroNames:Array = controller.hashMap.getNames();
					for( var i:int = 0; i < macroNames.length; i++)
					{
						if( event.textInput == macroNames[i])
						{
							var box:AlertTextBox = AlertTextBox.show(lang("Macro Naming Window"), lang("The name you entered is already being used. Please enter a new name:"));
							box.addEventListener(AlertTextBoxEvent.BUTTON_CLICKED,checkInput);
							return;
						}
					}
					if( renaming )
					{
						controller.hashMap.renameObject(selectedFunctionName, event.textInput);
						macroList.selectedIndex = lastSelectedIndex;
						renaming = false;
						return;
					}
					controller.hashMap.requestObject(event.textInput, LinkableFunction, false);
					editor.text = "";
					macroList.selectedIndex = controller.hashMap.getNames().length - 1;
					if (selectedFunction)
						editor.editable = true;
				}
			}
			
			protected function macroList_itemClickHandler(event:ListEvent):void
			{
				if (selectedFunction)
				{
					editor.editable = true;
					macroResult.text = "";
					editor.text = selectedFunction.value;	
					handleChange(null);
				}
				else
				{
					editor.editable = false;
					editor.text = "";
					macroResult.text = "";
				}
			}
			
			protected function handleChange(event:Event = null):void
			{
				if (selectedFunction)
				{
					try
					{
						var funcToSave:LinkableFunction = selectedFunction;	
						funcToSave.value = editor.text;
						macroResult.text = LinkableFunction.evaluateMacro(selectedFunctionName);
					}
					catch(e:Error)
					{
						macroResult.text = e.toString();
					}
				}
			}
			
			//Used to flag that there is a renaming going on.
			private var renaming:Boolean = false;
			//Use to set the selectedIndex back to where it should be once renaming is completed.
			private var lastSelectedIndex:int = 0;
			
			protected function renameMacroEntry(event:MouseEvent):void
			{
				if (selectedFunction)
				{
					renaming = true;
					lastSelectedIndex = macroList.selectedIndex;
					var box:AlertTextBox = AlertTextBox.show(lang("Macro Renaming Window"), lang("Enter a new name for the macro:"));
					box.addEventListener(AlertTextBoxEvent.BUTTON_CLICKED,checkInput);
				}
				else
				{
					mx.controls.Alert.show("Please select one item to rename.");
				}
			}
			
			override public function revert():void
			{
				super.revert();
				if( controller.hashMap.getNames(LinkableFunction).length > 0 )
					editor.text = selectedFunction.value;
			}
			
			protected function enableButtonsCheck():void
			{
				if( controller.hashMap.getNames(LinkableFunction).length > 0 )
				{
					buttonsEnabled = true;
					editor.editable = true;
				}
				else
				{
					buttonsEnabled = false;	
					editor.editable = false;
				}
			}
			
		]]>
	</mx:Script>
</ui:ControlPanel>