<?xml version="1.0" encoding="utf-8"?>
<!--
/*
	Weave (Web-based Analysis and Visualization Environment)
	Copyright (C) 2008-2011 University of Massachusetts Lowell
	
	This file is a part of Weave.
	
	Weave is free software: you can redistribute it and/or modify
	it under the terms of the GNU General Public License, Version 3,
	as published by the Free Software Foundation.
	
	Weave is distributed in the hope that it will be useful,
	but WITHOUT ANY WARRANTY; without even the implied warranty of
	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
	GNU General Public License for more details.
	
	You should have received a copy of the GNU General Public License
	along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->

<!---
@author skolman
@author kmonico
@author sanbalag
-->
<ui:DraggablePanel xmlns="weave.visualization.tools.*"
				   xmlns:mx="http://www.adobe.com/2006/mxml"
				   xmlns:ui="weave.ui.*" 
				   xmlns:CustomDataGrid="weave.ui.CustomDataGrid.*"
				   layout="absolute" implements="weave.api.ui.IVisTool_Basic, weave.api.ui.IObjectWithSelectableAttributes"
				   title="{lang('Table')}" xmlns:editors="weave.editors.*"
				   >
	<mx:VBox width="100%" height="100%" id="tableArea" verticalGap="0" horizontalScrollPolicy="off">
		<CustomDataGrid:CustomDataGrid
			id="dataGrid"
			horizontalScrollPolicy="auto"
			verticalScrollPolicy="auto"
			dataProvider="{dataRowKeys}" 
			width="100%" height="100%"
			allowMultipleSelection="true"
			minWidth="0"
			change="handleItemSelection(event)"
			doubleClick="handleItemSelection(event)" doubleClickEnabled="true" 
			headerShift="handleColumnReorder(event)"
			headerRelease="handleHeaderRelease(event)"
			paddingBottom="0" paddingTop="0" paddingLeft="0" paddingRight="0"									   
			fontWeight="normal"
			alternatingItemColors="{[0xffffff, 0xEEF6FF]}"
			rollOverColor="0xDADAFF"
			selectionColor="0xFFAAAA"
			mouseDown="handleMouseDown()"
			click="handleMouseClick()"
			/>
		<mx:HBox id="tooManyRecordsIndicator" visible="false" includeInLayout="false" width="100%" horizontalGap="0">
			<mx:Spacer width="100%"/>
			<mx:Label id="maxRowsLabel"/>
			<ui:HelpComponent>
				The table is currently hiding some of the records,
				as the amount has exceeded the tables capability for dealing with it efficiently.
			</ui:HelpComponent>
		</mx:HBox>
	</mx:VBox>
	
	<mx:Number id="columnWidthScale">1</mx:Number>
	
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.controls.dataGridClasses.DataGridColumn;
			import mx.core.mx_internal;
			import mx.events.DataGridEvent;
			import mx.events.IndexChangedEvent;
			import mx.events.ListEvent;
			import mx.events.ResizeEvent;
			import mx.managers.IFocusManagerComponent;
			
			import weave.Weave;
			import weave.api.copySessionState;
			import weave.api.core.ILinkableObject;
			import weave.api.data.DataType;
			import weave.api.data.IAttributeColumn;
			import weave.api.data.IQualifiedKey;
			import weave.api.detectLinkableObjectChange;
			import weave.api.getCallbackCollection;
			import weave.api.linkBindableProperty;
			import weave.api.newLinkableChild;
			import weave.api.registerLinkableChild;
			import weave.api.reportError;
			import weave.api.ui.IVisTool;
			import weave.compiler.StandardLib;
			import weave.core.LinkableBoolean;
			import weave.core.LinkableHashMap;
			import weave.core.LinkableNumber;
			import weave.core.LinkableString;
			import weave.core.UIUtils;
			import weave.data.AttributeColumns.DynamicColumn;
			import weave.data.AttributeColumns.KeyColumn;
			import weave.data.KeySets.KeyFilter;
			import weave.data.KeySets.KeySet;
			import weave.primitives.ColorRamp;
			import weave.ui.CustomDataGrid.DataGridColumnForQKey;
			import weave.ui.DraggablePanel;
			import weave.utils.ColumnUtils;
			import weave.utils.LinkableTextFormat;
			import weave.utils.NumberUtils;
			import weave.utils.ProbeTextUtils;
			use namespace mx_internal;
			
			WeaveAPI.ClassRegistry.registerImplementation(IVisTool, TableTool, "Table");
			
			public const colorRamp:ColorRamp = newLinkableChild(this, ColorRamp);
			public const columns:LinkableHashMap = registerLinkableChild(this, new LinkableHashMap(IAttributeColumn));
			public const columnWidths:LinkableHashMap = registerLinkableChild(this, new LinkableHashMap(LinkableString));
			public const enableProbing:LinkableBoolean = registerLinkableChild(this, new LinkableBoolean(true));

			private const dataGridColumns:Dictionary = new Dictionary();
			private var _currentDataGridColumns:Array = [];
			
			public const dataGridHorizontalScrollPosition:LinkableNumber = registerLinkableChild(this, new LinkableNumber(0));
			public const dataGridVerticalScrollPosition:LinkableNumber = registerLinkableChild(this, new LinkableNumber(0));
			
			private var _textFormat:LinkableTextFormat = Weave.properties.visTextFormat;
			private var _subset:KeyFilter = Weave.defaultSubsetKeyFilter;
			private var _selection:KeySet = Weave.defaultSelectionKeySet;
			private var _probe:KeySet = Weave.defaultProbeKeySet;

			public const sortFieldIndex:LinkableNumber = registerLinkableChild(this, new LinkableNumber(0));
			public const sortInDescendingOrder:LinkableBoolean = registerLinkableChild(this, new LinkableBoolean(false));
			
			private const dataRowKeys:ArrayCollection = new ArrayCollection([]);
			
			public const maxRows:LinkableNumber = registerLinkableChild(this, new LinkableNumber(25000));
			public const maxHeaderHeight:LinkableNumber = registerLinkableChild(this, new LinkableNumber(50));
			
			override protected function inConstructor():void
			{
				super.inConstructor();
				
				var probeHeaderColumns:Array = ProbeTextUtils.probeHeaderColumns.getObjects(IAttributeColumn);
				var probedColumns:Array = ProbeTextUtils.probedColumns.getObjects(IAttributeColumn);
				var allColumns:Array = (probeHeaderColumns.concat as Function).apply(null, probedColumns);
				for (var i:int = 0; i < allColumns.length; i++)
				{
					var dynamicColumn:DynamicColumn = columns.requestObject(null, DynamicColumn, false);
					var probedColumn:ILinkableObject = allColumns[i] as ILinkableObject;
					if (probedColumn is DynamicColumn)
						copySessionState(probedColumn, dynamicColumn);
					else
						dynamicColumn.requestLocalObjectCopy(probedColumn);
				}
				
				_probe.addImmediateCallback(this,updateGlobalProbeChangeLater);
				for each (var target:ILinkableObject in [this, _subset, _selection, Weave.defaultColorColumn, _textFormat])
					getCallbackCollection(target).addGroupedCallback(this, groupedUpdate);
			}
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();
				
				linkBindableProperty(enableProbing, enableProbingCheckBox, "selected");

				linkBindableProperty(dataGridHorizontalScrollPosition, dataGrid, 'horizontalScrollPosition', 0, true);
				linkBindableProperty(dataGridVerticalScrollPosition, dataGrid, 'verticalScrollPosition', 0, true);
				
				
				dataGrid.addEventListener(ListEvent.ITEM_ROLL_OVER, handleRollOver);
				dataGrid.addEventListener(MouseEvent.ROLL_OUT, handleRollOut);

				dataGrid.addEventListener(DataGridEvent.COLUMN_STRETCH, handleColumnStretch);
				dataGrid.addEventListener(ResizeEvent.RESIZE, handleToolResize);
				
				columns.childListCallbacks.addImmediateCallback(this, handleColumnListChange,true);
				columnWidths.childListCallbacks.addImmediateCallback(this, assertColumnOrder,true);
			}

			private function assertColumnOrder():void
			{
				columnWidths.setNameOrder(columns.getNames());
			}
			
			/**
			 * This function(event ListeSner) gets called during dataGrid Resize event
			 * handleDataGridHorizontalScroll calls updateColumnWidths, when the dataGridHorizontalScrollPosition.value is Maximum
			 * handleDataGridHorizontalScroll is called to update FilterComponents Position
			 */			
			private function handleToolResize(event:Event = null):void
			{
				updateColumnWidths();
			}
			
			private function handleRollOver(event:ListEvent):void // Probe
			{
				if (enableProbing.value == false)
					return;

				var index:int = event.rowIndex;
				var obj:Object = (dataGrid.dataProvider as ArrayCollection).getItemAt( index );
				var key:IQualifiedKey = obj as IQualifiedKey;

				_probe.replaceKeys([key]);
			}
			private function handleRollOut(event:MouseEvent):void // Unprobe
			{
				if (enableProbing.value == false)
					return;
				
				_probe.clearKeys();
			}
			
			private function groupedUpdate():void
			{
				if (!parent) 
					return;
				
				var columnsChanged:Boolean = detectLinkableObjectChange(groupedUpdate, columns);
				if (columnsChanged)
				{
					updateDataGridColumns();
					super.updateDisplayList(unscaledWidth, unscaledHeight); // avoids crashing below
				}
				if (detectLinkableObjectChange(groupedUpdate, lockedColumns) || columnsChanged)
				{
					handleLockedColumnChange();
				}
				if (detectLinkableObjectChange(groupedUpdate, maxRows) || columnsChanged)
				{
					updateKeysColumnsDataProviderAndSelection();
				}
				if (columnsChanged)
				{
					updateBorders();
				}
				if (detectLinkableObjectChange(groupedUpdate, columnWidths, maxHeaderHeight))
				{
					updateColumnWidths();
				}
				if (detectLinkableObjectChange(groupedUpdate, dataGridHorizontalScrollPosition) || columnsChanged)
				{
					handleDataGridHorizontalScroll();
				}
				if (detectLinkableObjectChange(groupedUpdate, dataGridVerticalScrollPosition) || columnsChanged)
				{
					if (dataGrid.verticalScrollPosition != dataGridVerticalScrollPosition.value)
						dataGrid.verticalScrollPosition = dataGridVerticalScrollPosition.value;
				}
				var subsetChanged:Boolean = detectLinkableObjectChange(groupedUpdate, _subset);
				if (subsetChanged)
				{
					dataRowKeys.filterFunction = _subset.containsKey;
					dataRowKeys.refresh();
				}
				
				if (detectLinkableObjectChange(groupedUpdate, _selection))
				{
					updateGlobalSelectionChange();
					dataGrid.invalidateList();
				}
				
				if (detectLinkableObjectChange(groupedUpdate, _textFormat))
				{
					_textFormat.copyToStyle(dataGrid);
				}
				if (detectLinkableObjectChange(groupedUpdate, sortFieldIndex, sortInDescendingOrder) || columnsChanged || subsetChanged)
				{
					callLater(sortCallback);
				}
				if (detectLinkableObjectChange(groupedUpdate, backgroundColor1, backgroundColor2, colorRamp, Weave.defaultColorColumn))
				{
					dataGrid.setStyle("alternatingItemColors", [backgroundColor1.value, backgroundColor2.value]);
					extraColorTriggers = 2;
				}
				if (extraColorTriggers)
				{
					extraColorTriggers--;
					callLater(updateColors);
				}
				updateGlobalProbeChangeLater();
			}
			
			private var extraColorTriggers:uint = 2;
			private function updateColors():void
			{
				dataGrid.styleChanged('alternatingItemColors');
			}
			
			override public function get defaultPanelTitle():String
			{
				return lang("Table");
			}
			
			public function getSelectableAttributes():Array
			{
				return [columns];
			}
			
			public function getSelectableAttributeNames():Array
			{
				return ["Columns"];
			}
			
			
			/**
			 * This function(event Listener) gets called during 
			 * HeaderRelease event in Datagrid 
			 * or Called (Manually )when column or sortFieldIndex or sortInDescendingOrder changes
			 */
			private function handleHeaderRelease(event:DataGridEvent):void
			{
				// There is a bug in flex: column.sortDescending will always return false				
				//need to update sessionState when called by User Interaction
				if (UIUtils.hasFocus(dataGrid))
				{
					sortInDescendingOrder.value = !isSortedDescending(); // each time will toggle ascending/descending
					sortFieldIndex.value = event.columnIndex;
				}
				
			}
			
			private function isSortedDescending():Boolean
			{
				return dataGrid.sortDirection != 'ASC';
			}
			
			/**
			 * called from callLater of handleHeaderRelease listener ,to get the correct sorting order. 
			 * There is a bug in flex because of which the c.sortDescending will always return false
			 */
			private function getOrder(column:DataGridColumn,index:int):void
			{
			}
			
			/**
			 * This function gets called in Updatedisplaylist, 
			 * when there is change in sortFieldIndex or sortInDescending 	
			 * called with parameter if called from change in column 
			 */							
			private function sortCallback():void
			{
				if (!parent)
				{
					callLater(sortCallback)
					return;
				}
				
				if (isNaN(sortFieldIndex.value))
					return;
				
				var c:DataGridColumn = dataGrid.columns[sortFieldIndex.value];
				if (c == null)
					return;
				
				// sort by the column specified in the session state , flip the sorting order if it is wrong
				// each time we dispatch a header event, the sort order gets flipped, so we may have to flip it again
				var changeDir:Boolean = isSortedDescending() != sortInDescendingOrder.value;
				var changeIndex:Boolean = dataGrid.sortIndex != sortFieldIndex.value;
				var times:int = 0;
				if (changeDir)
					times = 1; // change direction only requires one event
				else if (changeIndex)
					times = 2; // (change index and keep direction) requires two events

				var prevFocus:IFocusManagerComponent = focusManager.getFocus();
				for (var i:int = 0 ; i < times; i++)
					dataGrid.dispatchEvent(new DataGridEvent(DataGridEvent.HEADER_RELEASE, false, true, sortFieldIndex.value, null, 0, null, null, 0));
				if (prevFocus)
					focusManager.setFocus(prevFocus);
			}
			
			/**
			 * This function(event Listener) gets called during 
			 * Change event in DataGrid
			 * DoubleClick event in DataGrid
			 */	
			private function handleItemSelection(event:Event=null):void
			{
				dataGrid.validateNow();
				var selectedKeys:Array = new Array();
				for (var i:int = 0; i < dataGrid.selectedIndices.length; i++)
				{
					selectedKeys[i] = (dataGrid.dataProvider as ArrayCollection).getItemAt( dataGrid.selectedIndices[i] ) as IQualifiedKey;
				}
				_selection.replaceKeys(selectedKeys);
			}
			
//			private function clearSelection():void
//			{
//				_selection.clearKeys();
//			}
			/* TODO: Move this method into an abstract class to extend CustomDataGrid off of;
			   this functionality is duplicated used in TreeTool */ 
			private function updateGlobalSelectionChange():void
			{
				if (!parent)
					return callLater(updateGlobalSelectionChange);
				//flex will crash if we try to select items immediately
				if (dataGrid.visibleLockedColumns && lockedColumns.value == 0 && dataGrid.visibleLockedColumns.length != 0)
					validateNow();
				
				var item:Object;
				var isSelected:Dictionary = new Dictionary(true);
				for each (item in dataGrid.selectedItems)
					isSelected[item] = true;
				
				var changeDetected:Boolean = false;
				var selectedItems:Array = [];
				for each (item in dataGrid.dataProvider)
				{
					if (_selection.containsKey(item as IQualifiedKey))
					{
						if (!isSelected[item])
							changeDetected = true;
						selectedItems.push(item);
					}
					else if (isSelected[item])
						changeDetected = true;
				}
				if (selectedItems.length != dataGrid.selectedItems.length)
					changeDetected = true;
				
				if (changeDetected)
					dataGrid.selectedItems = selectedItems;
			}
			private function updateGlobalProbeChangeLater():void
			{
				if (enableProbing.value == true)
					callLater(updateGlobalProbeChange);
			}
			/* TODO: Move this method into an abstract class to extend CustomDataGrid off of;
			   this functionality is duplicated used in TreeTool */ 
			private function updateGlobalProbeChange():void 
			{
				if (!parent)
				{
					callLater(updateGlobalProbeChange);
					return;
				}
				dataGrid.invalidateList();
				dataGrid.validateNow();
				dataGrid.highlightItemsForced(_probe.keys, _selection.containsKey);
			}
			
			/**
			 * @TODO This should be a mode, not a one-time action.
			 *       Use AdvancedDataGrid and lockedRowCount.
			 *       The locked rows should also be sorted by the active sort column.
			 *       We shouldn't be resetting the sort.
			 */
			private function moveSelectedToTop():void
			{
				//push selected Objects into an Array
				var selectedItems:Array = dataGrid.selectedItems;
				
				//datagrid.selectedItems returns null. So have to loop through indices to get the items
				(dataGrid.dataProvider as ArrayCollection).sort = null;
				
				for each(var item:Object in selectedItems)
				{
					var index:int = dataRowKeys.getItemIndex(item);
					dataRowKeys.removeItemAt(index);
					dataRowKeys.source.unshift(item);
				}
				
				dataRowKeys.refresh();
				dataGrid.validateNow();
				updateGlobalSelectionChange();
				dataGrid.scrollToIndex(0);
			}
			
			private static const COMBO_ITEM_ALL_KEY_TYPES:String = lang("All key types");
			
			private function updateKeysColumnsDataProviderAndSelection():void
			{
				var attrCols:Array = dataGrid.columns.map(function(dgc:DataGridColumnForQKey,..._):*{ return dgc.attrColumn; });
				var keys:Array = ColumnUtils.getAllKeys(attrCols);
				if (keys.length > maxRows.value)
				{
					maxRowsLabel.text = lang('Showing first {0} out of {1} records', StandardLib.formatNumber(maxRows.value), StandardLib.formatNumber(keys.length));
					tooManyRecordsIndicator.visible = true;
					tooManyRecordsIndicator.includeInLayout = true;
					keys.length = maxRows.value;
				}
				else
				{
					tooManyRecordsIndicator.visible = false;
					tooManyRecordsIndicator.includeInLayout = false;
				}
					
				dataRowKeys.source = keys;
				updateGlobalSelectionChange();
			}
			
			private var _minSuggestedColumnSize:int = 32;
			
			private function updateDataGridColumns():void
			{
				// if there isn't a parent or the grid isn't fully instantiated, return
				if (!parent)
					return callLater(updateDataGridColumns);
				try
				{
					var attrColumns:Array = columns.getObjects();
					_currentDataGridColumns = [];
					for each (var attrColumn:IAttributeColumn in attrColumns)
					{
						var columnName:String = columns.getName(attrColumn);
						var dataType:String = ColumnUtils.getDataType(attrColumn);
						
						//column Section
						var dgc:DataGridColumnForQKey;
						if (dataGridColumns[columnName])
						{
							dgc = dataGridColumns[columnName];
						}
						else
						{
							dgc = new DataGridColumnForQKey(attrColumn);
							dataGridColumns[columnName] = dgc;
						}
						
						_currentDataGridColumns.push(dgc);
						
						//columnWidth section
						columnWidths.requestObject(columnName, LinkableString, false);
						
					}  
					dataGrid.columns = _currentDataGridColumns;
					numLockedColumns.maximum = Math.max(0, attrColumns.length - 2);
					numLockedColumns.enabled = numLockedColumns.maximum > 0;
				}
				catch (error:Error)
				{
					reportError(error);
				}
			}
			
			private function handleColumnReorder(event:IndexChangedEvent):void
			{
				var newColumnOrder:Array = [];
				for(var i:int = 0; i< dataGrid.columns.length; i++)
				{
					var currDGcolumn:DataGridColumnForQKey = dataGrid.columns[i] as DataGridColumnForQKey;
					var columnName:String = columns.getName(currDGcolumn.attrColumn as ILinkableObject);
					newColumnOrder.push(columnName);
				}
				columns.setNameOrder(newColumnOrder);
			}
			
			private function handleColumnListChange():void
			{
				showKeyColumnCheckBox.selected = columns.getNames(KeyColumn).length > 0;
				
				//if column is added , we need to add respective ColumnWidth
				if (columns.childListCallbacks.lastObjectAdded)
				{
				}  			
				//if column is removed , we need to remove respective ColumnWidth
				else if (columns.childListCallbacks.lastObjectRemoved)
				{
					delete dataGridColumns[columns.childListCallbacks.lastNameRemoved]
					columnWidths.removeObject(columns.childListCallbacks.lastNameRemoved);
				}
				assertColumnOrder();
			}
			
			private function handleColumnStretch(event:Event):void
			{
				var dataGridColumnsWidth:Number = dataGrid.getColumnDisplayWidth();
				for (var i:int = 0; i < _currentDataGridColumns.length; ++i)
				{
					var dgc:DataGridColumnForQKey = _currentDataGridColumns[i] as DataGridColumnForQKey;
					var colWidth:Number = dgc.width;
					
					var column:IAttributeColumn = dgc.attrColumn;
					var columnName:String = columns.getName(column);
					var sessionedWidth:LinkableString = columnWidths.requestObject(columnName, LinkableString, false);
					
					if (isNaN(colWidth) || width <= _minSuggestedColumnSize) // this happens when a column is stretched all the way to the right
						colWidth = _minSuggestedColumnSize;
					
					sessionedWidth.value = (100 * colWidth / dataGridColumnsWidth) + "%";
				}
			}
			
			private  function updateColumnWidths():void
			{
				// if there isn't a parent or the grid isn't fully instantiated, return
				if (!parent || dataGrid.width == 0)
				{
					callLater(updateColumnWidths);
					return;
				}
				var allottedDisplayWidth:Number = dataGrid.getColumnDisplayWidth();
				var columnName:String;
				var sessionedWidth:LinkableString;
				var defaultValue:Number = allottedDisplayWidth/_currentDataGridColumns.length;
				var displayedColumnsTotalWidth:Number = 0;
				for (var j:int = 0; j < _currentDataGridColumns.length; j++)
				{
					var dgc:DataGridColumnForQKey = _currentDataGridColumns[j] as DataGridColumnForQKey;
					columnName = columns.getName(dgc.attrColumn);
					if (columnName == null)
						continue;
					sessionedWidth = columnWidths.getObject(columnName) as LinkableString;
					var tempWidth:Number;
					// when we reset width, we need all columns to have default value
					 if (sessionedWidth && sessionedWidth.value)
					 {
						 var sessionedValue:Number = NumberUtils.getNumberFromNumberOrPercent(sessionedWidth.value, allottedDisplayWidth);
						 tempWidth = sessionedValue;
					 }
					 else
					 {
						 tempWidth = defaultValue;
						 //sessionedWidth.value = (100 * tempWidth/allottedDisplayWidth) + "%";
					 } 
					
					//last column gets width to adjust in datgrid, so its necessay to get that width.					
					if (j == _currentDataGridColumns.length -1) 
						tempWidth = allottedDisplayWidth - displayedColumnsTotalWidth;
					if (tempWidth <= _minSuggestedColumnSize || isNaN(tempWidth))
						tempWidth = _minSuggestedColumnSize;
					if (tempWidth >= allottedDisplayWidth - _minSuggestedColumnSize)
						tempWidth = allottedDisplayWidth - _minSuggestedColumnSize;
					//int casting required as rendering works fast with whole number than with an floating number
					var newWidth:Number = int(Math.min(tempWidth, allottedDisplayWidth));
					var lastColumn:Boolean = j == _currentDataGridColumns.length - 1;
					if (lastColumn)
						newWidth -= SCROLLBAR_WIDTH; // account for scrollbar
					
					dgc.setWidth(newWidth);
					dgc.width = newWidth;
					
					//add those columns which are displayed on datagrid
					if (j >= dataGridHorizontalScrollPosition.value)
					{
						displayedColumnsTotalWidth = displayedColumnsTotalWidth + dgc.width;
					}
				}
				displayedColumnsTotalWidth = 0;
				
				// Set max header height
				dataGrid.headerHeight = NaN;
				dataGrid._explicitHeaderHeight = false;
				if (dataGrid.calculateHeaderHeight() > maxHeaderHeight.value)
					dataGrid.headerHeight = maxHeaderHeight.value;
			}
			
			private function handleDataGridHorizontalScroll():void
			{
				var maximumValue:int = dataGrid.maxHorizontalScrollPosition;
				var hsp:int = 0;
				for (var i:int = 0; i < dataGridHorizontalScrollPosition.value && i < _currentDataGridColumns.length; i++)
				{
					hsp += (_currentDataGridColumns[i] as DataGridColumnForQKey).width;
				}
				dataGrid.horizontalScrollPosition = dataGridHorizontalScrollPosition.value;
				
				if (maximumValue > 0 && dataGridHorizontalScrollPosition.value == maximumValue)
				{
					updateColumnWidths();
				}
			}
			
			
			private function handleKeyColumnCheckBox():void
			{
				if (showKeyColumnCheckBox.selected)
				{
					var kc:Array= columns.getObjects(KeyColumn);
					if (kc.length == 0)
					{
						var nameOrder:Array = columns.getNames();
						columns.requestObject("KeyColumn",KeyColumn, false);
						columns.setNameOrder(nameOrder);
					}
				}
				else
				{
					var keyCols:Array = columns.getNames(KeyColumn);
					for each(var keyCol:String in keyCols)
					{
						columns.removeObject(keyCol);
					}
				}
				
			}
			
			public const lockedColumns:LinkableNumber = registerLinkableChild(this, new LinkableNumber(0, verifyLockedColumnCount));
			
			private function verifyLockedColumnCount(value:Number):Boolean
			{
				return value == uint(value);
			}
			
			private function handleLockedColumnChange():void
			{
				var numColumns:int = dataGrid.columns.length;
				if (lockedColumns.value >= numColumns)
					dataGrid.lockedColumnCount = 0;
				else
					dataGrid.lockedColumnCount = lockedColumns.value;
			}
			
			private var _previousSelection:Object ;
			
			private function handleMouseDown():void
			{
				detectLinkableObjectChange( handleMouseClick, _selection )
			}
			
			private function handleMouseClick():void
			{
				if (!detectLinkableObjectChange( handleMouseClick, _selection ))
					if ( dataGrid.mouseY > dataGrid.headerHeight )
						if ( dataGrid.mouseX < dataGrid.width - SCROLLBAR_WIDTH)
							if ( WeaveAPI.StageUtils.pointClicked )
								_selection.clearKeys() ;
			}
			private static const SCROLLBAR_WIDTH:int = 16;
			
			private static const DEFAULT_BACKGROUND_COLORS:Array = [0xffffff, 0xEEF6FF];
			public const backgroundColor1:LinkableNumber = registerLinkableChild(this, new LinkableNumber(DEFAULT_BACKGROUND_COLORS[0]));
			public const backgroundColor2:LinkableNumber = registerLinkableChild(this, new LinkableNumber(DEFAULT_BACKGROUND_COLORS[1]));
		]]>
	</mx:Script>
	<ui:ControlPanel>
		<mx:VBox label="{lang('Columns')}">
			<mx:CheckBox id="showKeyColumnCheckBox" label="{lang('Show key column')}" change="handleKeyColumnCheckBox()"/>
			<ui:ColumnListComponent id="dimensionsList" creationComplete="dimensionsList.hashMap = columns;"/>
			<mx:HBox>
				<mx:Label text="{lang('Number of locked columns')}"/>
				<mx:NumericStepper id="numLockedColumns" minimum="0" 
								   creationComplete="linkBindableProperty(lockedColumns, event.target, 'value', 0, true);"/>
			</mx:HBox>

			<mx:Button label="{lang('Reset column widths')}" click="columnWidths.removeAllObjects()"/>

			<mx:CheckBox id="enableProbingCheckBox" label="{lang('Enable mouseover linking')}"/>
			
			<ui:Indent id="bgColorControls" label="{lang('Background colors')}">
				<ui:CustomColorPicker creationComplete="linkBindableProperty(backgroundColor1, event.target, 'selectedColor');"/>
				<ui:CustomColorPicker creationComplete="linkBindableProperty(backgroundColor2, event.target, 'selectedColor');"/>
				<mx:Button label="{lang('Reset')}" click="backgroundColor1.value = DEFAULT_BACKGROUND_COLORS[0]; backgroundColor2.value = DEFAULT_BACKGROUND_COLORS[1];"/>
			</ui:Indent>
		</mx:VBox>
		<editors:WindowSettingsEditor id="windowEditor" target="{this}" creationComplete="windowEditor.addChild(bgColorControls);"/>
	</ui:ControlPanel>
</ui:DraggablePanel>
