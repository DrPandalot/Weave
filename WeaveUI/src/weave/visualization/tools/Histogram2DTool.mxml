<?xml version="1.0" encoding="utf-8"?>
<!--
/*
Weave (Web-based Analysis and Visualization Environment)
Copyright (C) 2008-2011 University of Massachusetts Lowell

This file is a part of Weave.

Weave is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License, Version 3,
as published by the Free Software Foundation.

Weave is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<SimpleVisTool xmlns="weave.visualization.tools.*" 
			   xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:ui="weave.ui.*" 
			   xmlns:userControls="weave.ui.userControls.*"
			   xmlns:settings="weave.ui.settings.*"
			   layout="{new BasicLayout()}">
	<fx:Script>
		<![CDATA[
			import spark.layouts.BasicLayout;
			
			import weave.Weave;
			import weave.api.WeaveAPI;
			import weave.api.getCallbackCollection;
			import weave.api.linkBindableProperty;
			import weave.api.linkSessionState;
			import weave.api.registerLinkableChild;
			import weave.api.unlinkSessionState;
			import weave.api.ui.IVisTool;
			import weave.core.LinkableBoolean;
			import weave.data.AttributeColumns.BinnedColumn;
			import weave.ui.AttributeSelectorPanel;
			import weave.ui.ColorController;
			import weave.ui.DraggablePanel;
			import weave.utils.BitmapText;
			import weave.utils.ColumnUtils;
			import weave.visualization.plotters.Histogram2DPlotter;
			
			WeaveAPI.registerImplementation(IVisTool, Histogram2DTool, lang("Histogram 2D"));
			
			private function get plotter():Histogram2DPlotter
			{
				return mainPlotter as Histogram2DPlotter;
			}
			override protected function constructor():void
			{
				super.constructor();
				
				// lock dynamic objects into place
				initializePlotters(Histogram2DPlotter, true);
				visualization.enableProbeLine(false, true);
				
				xAxisPlotter.forceTickCount.value = true;
				visualization.enableZoomAndPan.value = false;
				
				visualization.enableProbeLine(true, true);
				
				visualization.plotManager.marginBottom.value = "80";
				visualization.plotManager.marginTop.value    = "10";
				visualization.plotManager.marginLeft.value   = "80";
				visualization.plotManager.marginRight.value  = "10";
				
				visualization.plotManager.marginLeftNumber.addGroupedCallback(this, updateAxisLabels);
				visualization.plotManager.marginBottomNumber.addGroupedCallback(this, updateAxisLabels);
				getCallbackCollection(visualization.plotManager.zoomBounds).addGroupedCallback(this, updateAxisLabels);
				getCallbackCollection(mainPlotter).addGroupedCallback(this, updateAxisLabels, true);
				
				var self:* = this;
				visualization.bottomMarginClickCallbacks.addImmediateCallback(this, function():void { AttributeSelectorPanel.openToolSelector(self, plotter.xColumn); })
				visualization.leftMarginClickCallbacks.addImmediateCallback(this, function():void { AttributeSelectorPanel.openToolSelector(self, plotter.yColumn); })
				
				visualization.enableAutoZoomXToNiceNumbers.value = false;
				visualization.enableAutoZoomYToNiceNumbers.value = false;
				
				initColumnDefaults(plotter.xColumn, plotter.yColumn);
				
			}
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();
				
				cRampEditor.colorRamp = plotter.binColors;
				handleBinColRadioButtonChange();
				xColumnComponent.dynamicObject = plotter.xColumn;
				yColumnComponent.dynamicObject = plotter.yColumn;
				colorColumnComponent.dynamicObject = Weave.defaultColorDataColumn.internalDynamicColumn;
			}
			
			override public function getSelectableAttributeNames():Array
			{
				return ["X","Y"];
			}
			override public function getSelectableAttributes():Array
			{
				return [plotter.xColumn,plotter.yColumn];
			}
			
			public const includeToolAttributesInProbe:LinkableBoolean = registerLinkableChild(this, new LinkableBoolean(true), updateProbeColumns);
			
			
			private function updateProbeColumns():void
			{
				visualization.additionalProbeColumns = [];
				if (includeToolAttributesInProbe.value)
				{
					if (plotter.xBinnedColumn.getInternalColumn() && plotter.yBinnedColumn.getInternalColumn())
					{
						visualization.additionalProbeColumns.push(plotter.xBinnedColumn.getInternalColumn());
						visualization.additionalProbeColumns.push(plotter.xBinnedColumn.getInternalColumn());
					}
				}
			}
			
			private var _prevBinnedColumn:BinnedColumn = null;
			private function handleBinColRadioButtonChange():void
			{
				var selectedCol:String = binColGroup.selectedValue as String;
				
				//it is null when the tool loads for the first time and so it defaults to the x column
				if (selectedCol == "xBinCol" || selectedCol == null)
				{
					xBinColRadioButton.setStyle('fontWeight','bold');
					yBinColRadioButton.setStyle('fontWeight','normal');
					
					if (_prevBinnedColumn)
						unlinkSessionState(_prevBinnedColumn, binEditor.binnedColumn);
					
					if (plotter.xBinnedColumn)
						linkSessionState(plotter.xBinnedColumn, binEditor.binnedColumn);
					
					_prevBinnedColumn = plotter.xBinnedColumn;
				}
				else if (selectedCol == "yBinCol")
				{
					yBinColRadioButton.setStyle('fontWeight','bold');
					xBinColRadioButton.setStyle('fontWeight','normal');
					
					if (_prevBinnedColumn)
						unlinkSessionState(_prevBinnedColumn, binEditor.binnedColumn);
					
					if (plotter.yBinnedColumn)
						linkSessionState(plotter.yBinnedColumn, binEditor.binnedColumn);
					
					_prevBinnedColumn = plotter.yBinnedColumn;
				}
			}
			
			
			
			override protected function get defaultPanelTitle():String
			{
				return lang("2D Histogram of {0} vs {1}", ColumnUtils.getTitle(plotter.xColumn), ColumnUtils.getTitle(plotter.yColumn));
			}
			
			private function updateAxisLabels():void
			{
				handlePanelTitleChange();
				visualization.bottomMarginColumn = plotter.xColumn;
				visualization.leftMarginColumn = plotter.yColumn;
				
				var xBinCol:BinnedColumn = plotter.xBinnedColumn;
				
				xAxisPlotter.tickCountRequested.value = xBinCol.numberOfBins;
				xAxisPlotter.tickMinValue.value = 0;
				xAxisPlotter.tickMaxValue.value = xBinCol.numberOfBins - 1;	
				xAxisPlotter.setLabelFunction(xBinCol.deriveStringFromNumber);
				
				var yBinCol:BinnedColumn = plotter.yBinnedColumn;
				
				yAxisPlotter.tickCountRequested.value = yBinCol.numberOfBins;
				yAxisPlotter.tickMinValue.value = 0;
				yAxisPlotter.tickMaxValue.value = yBinCol.numberOfBins - 1;	
				yAxisPlotter.setLabelFunction(yBinCol.deriveStringFromNumber);
				
				
				xAxisPlotter.setSideAxisName(ColumnUtils.getTitle(plotter.xColumn), 0, 0, visualization.plotManager.marginBottomNumber.value - 3, BitmapText.VERTICAL_ALIGN_BOTTOM);
				yAxisPlotter.setSideAxisName(ColumnUtils.getTitle(plotter.yColumn), -90, -visualization.plotManager.marginLeftNumber.value, 0, BitmapText.VERTICAL_ALIGN_TOP);
			}
		]]>
	</fx:Script>
	<fx:Declarations>
		<s:RadioButtonGroup id="binColGroup" change="handleBinColRadioButtonChange()"/>
	</fx:Declarations>
	
	<ui:ControlPanel>
		<s:NavigatorContent label="{lang('Visualization Controls')}">
			<s:layout>
				<s:VerticalLayout/>
			</s:layout>
			<s:CheckBox id="probeColumnsCheckBox" label="{lang('Include tool attributes in probe tooltip')}"
						toolTip="{lang('Checking this box makes the tool\'s binned attribute visible in this tool\'s probe tooltip')}"
						creationComplete="linkBindableProperty(includeToolAttributesInProbe,event.target,'selected')"/>
			<ui:DynamicColumnComponent visTool="{this}" id="xColumnComponent" label="{lang('X Column')}"/>
			<ui:DynamicColumnComponent visTool="{this}" id="yColumnComponent" label="{lang('Y Column')}"/>
		</s:NavigatorContent>
		<s:NavigatorContent label="{lang('Binning Controls')}">
			<s:layout>
				<s:VerticalLayout/>
			</s:layout>
			<s:HGroup>
				<s:Label text="{lang('Select Column:')}"/>
				<s:RadioButton groupName="binColGroup" label="{lang('X Column')}" value="xBinCol" id="xBinColRadioButton" selected="true"/>
				<s:RadioButton groupName="binColGroup" label="{lang('Y Column')}" value="yBinCol" id="yBinColRadioButton"/>
			</s:HGroup>
			<ui:BinningDefinitionEditor id="binEditor"/>
		</s:NavigatorContent>
		<s:NavigatorContent label="{lang('Advanced')}">
			<s:layout>
				<s:VerticalLayout/>
			</s:layout>
			<s:CheckBox id="showAverageColorData" label="{lang('Color by average data value instead of record count')}"
						creationComplete="linkBindableProperty(plotter.showAverageColorData, event.currentTarget, 'selected');"/>
			<s:HGroup width="100%" verticalAlign="middle" enabled="{showAverageColorData.selected}">
				<ui:DynamicColumnComponent id="colorColumnComponent" visTool="{this}" label="{lang('Color data')}"/>
				<s:Button label="{lang('Edit colors...')}" click="DraggablePanel.openStaticInstance(ColorController);"/>
			</s:HGroup>
			<ui:ColorRampEditor id="cRampEditor" enabled="{!showAverageColorData.selected}"/>
		</s:NavigatorContent>
	</ui:ControlPanel>
</SimpleVisTool>
