<?xml version="1.0" encoding="utf-8"?>
<!--
/* ***** BEGIN LICENSE BLOCK *****
 *
 * This file is part of Weave.
 *
 * The Initial Developer of Weave is the Institute for Visualization
 * and Perception Research at the University of Massachusetts Lowell.
 * Portions created by the Initial Developer are Copyright (C) 2008-2015
 * the Initial Developer. All Rights Reserved.
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this file,
 * You can obtain one at http://mozilla.org/MPL/2.0/.
 * 
 * ***** END LICENSE BLOCK ***** */
-->
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" visible="false">
	<mx:VBox width="100%" horizontalAlign="center">
		<mx:Label id="title" text="{_fileName}" />
		<mx:VBox>
			<mx:HBox>
				<mx:Label text="Last Modified:" width="88" /><mx:Label text="{_lastMod}"/>
			</mx:HBox>
			<mx:HBox>
				<mx:Label text="File Size:" width="88" /><mx:Label text="{_fileSize}"/>
			</mx:HBox>
		</mx:VBox>
		<mx:Canvas minHeight="128">
			<mx:Image id="img" source="{_thumbByteArray}" buttonMode="true" useHandCursor="true" click="Admin.instance.openWeavePopup(_fileName)" />
		</mx:Canvas>
	</mx:VBox>
	<mx:Script>
		<![CDATA[
			import mx.controls.Image;
			import mx.formatters.NumberFormatter;
			import mx.rpc.events.ResultEvent;
			
			import weave.compiler.StandardLib;
			import weave.services.Admin;
			import weave.services.addAsyncResponder;
			import weave.services.beans.WeaveFileInfo;
			import weave.utils.FileUtils;
			
			[Bindable] private var _fileName:String = "";
			[Bindable] private var _thumbByteArray:ByteArray = null;
			[Bindable] private var _lastMod:String = "";
			[Bindable] private var _fileSize:String = "";
			
			private var formater:NumberFormatter = new NumberFormatter();

			public function clearPreview():void
			{
				visible = false;
				_fileName = '';
				_thumbByteArray = null;
				_lastMod = '';
				_fileSize = '';
			}
			public function updatePreview(fileName:String):void
			{
				clearPreview();
				_fileName = fileName;
				addAsyncResponder(Admin.service.getWeaveFileInfo(fileName), handleInfo, null, fileName);
			}
			private function handleInfo(event:ResultEvent, fileName:String):void
			{
				// ignore old requests
				if (_fileName != fileName)
					return;
				
				var info:WeaveFileInfo = WeaveFileInfo(event.result);
				
				visible = true;
				
				_thumbByteArray = info.thumb;
				_lastMod = StandardLib.formatDate(info.lastModified, "MM/DD/YYYY L:NN A", false);
				_fileSize = FileUtils.parse(info.fileSize, 2);
			}
			
		]]>
	</mx:Script>
</mx:VBox>
